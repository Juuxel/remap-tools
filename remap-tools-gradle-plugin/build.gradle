plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish'
}

sourceSets {
    loom {
        compileClasspath += main.compileClasspath
    }
}

dependencies {
    implementation project(':remap-tools')
    implementation libs.tinyremapper
    api libs.mappingio
    compileOnly libs.jetbrains.annotations

    loomImplementation sourceSets.main.output
    loomImplementation libs.loom
}

jar {
    from sourceSets.loom.output
}

sourcesJar {
    from sourceSets.loom.allSource
}

javadoc {
    classpath = sourceSets.main.compileClasspath + sourceSets.loom.compileClasspath
    source sourceSets.loom.allJava
}

gradlePlugin {
    plugins {
        core {
            id = 'io.github.juuxel.remap-tools'
            implementationClass = 'juuxel.remaptools.gradle.RemapToolsPlugin'
        }

        loom {
            id = 'io.github.juuxel.remap-tools.loom'
            implementationClass = 'juuxel.remaptools.gradle.loom.RemapToolsLoomPlugin'
        }
    }
}
